apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.appCompileSdkVersion
    buildToolsVersion project.ext.appBuildToolsVersion

    def latestGitCommitSha = "\"${getLatestGitCommitSha()}\""
    def latestBuildTime = "\"${getLatestBuildTime()}\""

    defaultConfig {
        applicationId project.ext.appApplicationId

        minSdkVersion project.ext.appMinSdkVersion
        targetSdkVersion project.ext.appTargetSdkVersion

        versionCode project.ext.appVersionCode
        versionName project.ext.appVersionName

        testInstrumentationRunner project.ext.appTestInstrumentationRunner

        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "latestGitCommitSha", latestGitCommitSha)
        buildConfigField("String", "latestBuildTime", latestBuildTime)

        buildConfigField("String", "GITHUB_API_BASE_URL", "\"${GithubApiBaseUrl}\"")

        printf("\n------------- " + applicationId + " -------------")
        printf("\nversionName: " + versionName)
        printf("\nversionCode: " + versionCode)

        printf("\nlatestGitCommitSha: " + latestGitCommitSha)
        printf("\nlatestBuildTime: " + latestBuildTime + "\n\n")
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " -" + latestGitCommitSha.tr('\"', ' ')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // google android dependencies
    implementation "com.android.support:appcompat-v7:${versions.supportLibrary}"
    implementation "com.android.support:cardview-v7:${versions.supportLibrary}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:design:${versions.supportLibrary}"

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    // 3rd party dependencies
    implementation "com.jakewharton:butterknife:${versions.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butterknife}"
    implementation "com.google.dagger:dagger:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${versions.dagger}"
    implementation "com.google.dagger:dagger-android:${versions.dagger}"
    implementation "com.google.dagger:dagger-android-support:${versions.dagger}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${versions.dagger}"
    implementation "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}"
    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${versions.retrofit}"
    implementation "io.reactivex.rxjava2:rxandroid:${versions.rxAndroid}"
    implementation "com.jakewharton.timber:timber:${versions.timber}"

    // unit test dependencies
    testImplementation "junit:junit:${versions.junit}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"

}
