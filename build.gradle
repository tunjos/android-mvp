import java.text.SimpleDateFormat

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    def versionMajor = 0
    def versionMinor = 1
    def versionPatch = 0
    def versionBuild = 0

    project.ext {
        appApplicationId = "co.tunjos.mvp"

        appBuildToolsVersion = "26.0.2"
        appCompileSdkVersion = 26
        appMinSdkVersion = 15
        appTargetSdkVersion = 26

        appVersionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        appVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

        appTestInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
    }

    ext {
        versions = [
                butterknife     : '8.8.1',
                dagger          : '2.14.1',
                okhttp          : '3.9.1',
                retrofit        : '2.3.0',
                rxandroid       : '2.0.1',
                timber          : '4.6.0'

        ]
    }
}

def static getLatestGitCommitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def static getLatestBuildTime() {
    def df = new SimpleDateFormat("dd-MM-yyyy HH:mm Z")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
